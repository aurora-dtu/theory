import MDP.Paths.Bounded
import MDP.Paths.Cost

open OmegaCompletePartialOrder

theorem iSup_iterate_succ' [CompleteLattice Î²] (f : (Î± â†’ Î²) â†’ Î± â†’ Î²) :
    â¨† n, f^[n + 1] âŠ¥ s = â¨† n, f^[n] âŠ¥ s :=
  le_antisymm (iSup_le_iff.mpr (le_iSup_of_le Â·.succ (by rfl))) <| iSup_le_iff.mpr fun n â†¦ by
    rcases n with n | n
    Â· simp
    Â· apply le_iSup_of_le n (by rfl)

theorem iSup_iterate_succ [CompleteLattice Î²] (f : (Î± â†’ Î²) â†’ Î± â†’ Î²) :
    â¨† n, f^[n + 1] âŠ¥ = â¨† n, f^[n] âŠ¥ := by
  ext
  simp
  exact iSup_iterate_succ' f

namespace MDP

variable {State : Type*} {Act : Type*}
variable (M : MDP State Act)

noncomputable def Î¦f (s : State) (a : Act) : (State â†’ ENNReal) â†’o ENNReal :=
  âŸ¨fun v â†¦ âˆ‘' s' : M.succs_univ s, M.P s a s' * v s', by
    intro v v' v_le_v'
    simp only
    gcongr
    exact v_le_v' _âŸ©

noncomputable def Î¦ğ’® (cost : M.Costs) (ğ’® : M.Scheduler) : (State â†’ ENNReal) â†’o (State â†’ ENNReal) :=
  âŸ¨fun v s â†¦ cost s + M.Î¦f s (ğ’® {s}) v, by
    intro v v' h s
    simp only
    gcongrâŸ©

noncomputable def actâ‚€_nonempty [M.FiniteBranching] (s : State ) : (M.actâ‚€ s).Nonempty :=
  Finset.nonempty_coe_sort.mp (instNonemptySubtypeMemFinsetActâ‚€ M s)

noncomputable def Î¦ (cost : M.Costs) : (State â†’ ENNReal) â†’o (State â†’ ENNReal) :=
  âŸ¨fun v s â†¦ cost s + â¨… Î± : M.act s, M.Î¦f s Î± v, by
    intro v v' h s
    simp only
    gcongrâŸ©

theorem Î¦.monotone' : Monotone (fun (cost : State â†’ ENNReal) â†¦ M.Î¦ cost) := by
  intro v v' h s
  apply add_le_add h; simp_all

theorem Î¦_le_Î¦ğ’® (cost : M.Costs) : M.Î¦ cost â‰¤ M.Î¦ğ’® cost ğ’® := by
  intro f s
  simp [Î¦, Î¦ğ’®]
  gcongr
  apply iInf_le_of_le âŸ¨ğ’® {s}, ğ’®.val.property' {s}âŸ©
  simp

noncomputable def lfp_Î¦ (cost : M.Costs) : State â†’ ENNReal := OrderHom.lfp (M.Î¦ cost)

noncomputable def iSup_Î¦ (cost : M.Costs) : State â†’ ENNReal := â¨† (n : â„•), (M.Î¦ cost)^[n + 1] âŠ¥
noncomputable def iSup'_Î¦ (cost : M.Costs) : State â†’ ENNReal := â¨† (n : â„•), (M.Î¦ cost)^[n] âŠ¥

theorem iSup_Î¦_eq_iSup'_Î¦ : M.iSup_Î¦ = M.iSup'_Î¦ := by
  ext cost s
  simp [iSup_Î¦, iSup'_Î¦]
  apply le_antisymm
  Â· simp
    intro n
    refine le_iSup_iff.mpr ?h.h.a.a
    intro e h
    have := h (n + 1)
    simp_all
  Â· simp
    intro n
    apply le_iSup_iff.mpr
    intro e h
    rcases n with n | n
    Â· simp_all
    Â· simp_all

theorem Î¦_iterate_succ (cost : M.Costs) (s : State) :
    (M.Î¦ cost)^[i + 1] âŠ¥ s
  = cost s + â¨… Î± : M.act s, M.Î¦f s Î± ((M.Î¦ cost)^[i] âŠ¥)
:= by
  simp only [Function.iterate_succ', Function.comp_apply, Î¦]
  simp
theorem Î¦ğ’®_iterate_succ (cost : M.Costs) (ğ’® : M.Scheduler) (s : State) :
    (M.Î¦ğ’® cost ğ’®)^[i + 1] âŠ¥ s
  = cost s + (M.Î¦f s (ğ’® {s})) ((M.Î¦ğ’® cost ğ’®)^[i] âŠ¥)
:= by
  simp only [Function.iterate_succ', Function.comp_apply, Î¦ğ’®]
  simp

theorem lfp_Î¦_step (cost : M.Costs) : M.Î¦ cost (M.lfp_Î¦ cost) = M.lfp_Î¦ cost :=
  OrderHom.map_lfp (M.Î¦ cost)

theorem lfp_Î¦_step' (cost : M.Costs) (s : State) :
    M.lfp_Î¦ cost s
  = cost s + (â¨… a : M.act s, âˆ‘' s' : M.succs_univ s, M.P s a s' * M.lfp_Î¦ cost s')
:= by
  conv => left ; rw [â†lfp_Î¦_step]
  simp [Î¦]
  congr! 2 with Î±

theorem lfp_Î¦_step_Î¦f (cost : M.Costs) (s : State) :
    M.lfp_Î¦ cost s
  = cost s + â¨… a : M.act s, M.Î¦f s a (M.lfp_Î¦ cost)
:= by
  conv => left ; rw [â†lfp_Î¦_step]
  simp [Î¦]

noncomputable def lfp_Î¦ğ’® (cost : M.Costs) (ğ’® : M.Scheduler) : State â†’ ENNReal :=
  OrderHom.lfp (M.Î¦ğ’® cost ğ’®)

theorem lfp_Î¦ğ’®_step (cost : M.Costs) (ğ’® : M.Scheduler) :
    M.Î¦ğ’® cost ğ’® (M.lfp_Î¦ğ’® cost ğ’®) = M.lfp_Î¦ğ’® cost ğ’® := OrderHom.map_lfp (M.Î¦ğ’® cost ğ’®)

theorem lfp_Î¦ğ’®_step_Î¦f (cost : M.Costs) (ğ’® : M.Scheduler) (s : State) :
    M.lfp_Î¦ğ’® cost ğ’® s
  = cost s + M.Î¦f s (ğ’® {s}) (M.lfp_Î¦ğ’® cost ğ’®)
:= by
  conv => left ; rw [â†lfp_Î¦ğ’®_step]
  simp [Î¦ğ’®]

theorem lfp_Î¦.mono (s : State) : Monotone (fun (cost : State â†’ ENNReal) â†¦ M.lfp_Î¦ cost s) :=
  fun _ _ h â†¦ OrderHomClass.GCongr.mono OrderHom.lfp (Î¦.monotone' M h) s

section FiniteBranching

variable [DecidableEq State] [M.FiniteBranching]

theorem Î¦f.Ï‰ScottContinuous (s) (a) : Ï‰ScottContinuous (M.Î¦f s a) := by
  refine Ï‰ScottContinuous.of_monotone_map_Ï‰Sup ?_
  use OrderHom.monotone (M.Î¦f s a)
  simp [Î¦f]
  intro c
  simp [Ï‰Sup, tsum_fintype, ENNReal.mul_iSup]
  refine ENNReal.finsetSum_iSup_of_monotone ?h.hf
  intro S iâ‚ iâ‚‚ h
  simp
  gcongr
  exact OrderHomClass.GCongr.mono c h S

theorem Î¦.Ï‰ScottContinuous (cost : M.Costs) : Ï‰ScottContinuous (M.Î¦ cost) := by
  refine Ï‰ScottContinuous.of_map_Ï‰Sup_of_orderHom ?_
  intro c
  ext s
  unfold Î¦
  simp [Ï‰Sup]
  simp [â† ENNReal.add_iSup]
  congr
  have := (Î¦f.Ï‰ScottContinuous M s Â· |>.map_Ï‰Sup)
  simp_all [Ï‰Sup]
  refine Eq.symm (Set.iSup_iInf_of_monotone ?h.e_a.hf)
  intro Î± iâ‚ iâ‚‚ h
  apply (M.Î¦f s Î±).mono <| OrderHomClass.GCongr.mono c h

theorem Î¦ğ’®.Ï‰ScottContinuous (cost : M.Costs) : Ï‰ScottContinuous (M.Î¦ğ’® cost ğ’®) := by
  refine Ï‰ScottContinuous.of_map_Ï‰Sup_of_orderHom ?_
  intro c
  ext s
  unfold Î¦ğ’®
  simp [Ï‰Sup]
  simp [â† ENNReal.add_iSup]
  congr
  have := (Î¦f.Ï‰ScottContinuous M s Â· |>.map_Ï‰Sup)
  simp_all [Ï‰Sup]

theorem lfp_Î¦_eq_iSup'_Î¦ : M.lfp_Î¦ = M.iSup'_Î¦ := by
  funext c
  exact fixedPoints.lfp_eq_sSup_iterate _ (Î¦.Ï‰ScottContinuous M c)

theorem lfp_Î¦ğ’®_eq_iSup_Î¦ğ’® : M.lfp_Î¦ğ’® = fun cost ğ’® â†¦ â¨† n, (M.Î¦ğ’® cost ğ’®)^[n] âŠ¥ := by
  funext c ğ’®
  exact fixedPoints.lfp_eq_sSup_iterate _ (Î¦ğ’®.Ï‰ScottContinuous M c)

theorem lfp_Î¦ğ’®_eq_iSup_succ_Î¦ğ’® : M.lfp_Î¦ğ’® = fun cost ğ’® â†¦ â¨† n, (M.Î¦ğ’® cost ğ’®)^[n + 1] âŠ¥ := by
  funext c ğ’®
  rw [lfp_Î¦ğ’®_eq_iSup_Î¦ğ’®, iSup_iterate_succ]

theorem lfp_Î¦_eq_iSup_Î¦ : M.lfp_Î¦ = M.iSup_Î¦ := M.lfp_Î¦_eq_iSup'_Î¦.trans M.iSup_Î¦_eq_iSup'_Î¦.symm

theorem iSup_Î¦_step (cost : M.Costs) : M.Î¦ cost (M.iSup_Î¦ cost) = M.iSup_Î¦ cost := by
  rw [â†lfp_Î¦_eq_iSup_Î¦]
  exact M.lfp_Î¦_step cost

theorem iSup_Î¦_step' (cost : M.Costs) (s : State) :
    M.iSup_Î¦ cost s
  = cost s + â¨… a : M.act s, âˆ‘' s' : M.succs_univ s, M.P s a s' * M.iSup_Î¦ cost s'
:= by
  rw [â†lfp_Î¦_eq_iSup_Î¦]
  exact M.lfp_Î¦_step' cost s
