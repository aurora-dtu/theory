import Mathlib.Data.ENNReal.Inv
import Mathlib.Data.ENNReal.Operations
import Mathlib.Order.FixedPoints
import Mathlib.Order.OmegaCompletePartialOrder
import MDP.Optimization
import STDX.Subst

/-! # Custom operators -/

@[notation_class]
class Validate (Î± : Type*) where
  /-- Validate `â–µ` -/
  validate : Î± â†’ Î±

@[notation_class]
class Covalidate (Î± : Type*) where
  /-- Co-validate `â–¿` -/
  covalidate : Î± â†’ Î±

export Validate (validate)
export Covalidate (covalidate)

/-- Heyting co-implication `â†œ` -/
notation:60 x:61 " â†œ " y:60 => y \ x
@[inherit_doc] prefix:72 "~ " => compl
@[inherit_doc] prefix:72 "â–µ " => validate
@[inherit_doc] prefix:72 "â–¿ " => covalidate

instance {Î± : Type*} [HNot Î±] : Validate Î± := âŸ¨fun x â†¦ ï¿¢ï¿¢xâŸ©
instance {Î± : Type*} [Compl Î±] : Covalidate Î± := âŸ¨fun x â†¦ xá¶œá¶œâŸ©

noncomputable instance {Î± Î² : Type*} [SDiff Î²] : SDiff (Î± â†’ Î²) := inferInstance

noncomputable instance : SDiff ENNReal := inferInstance
example {Ï† : ENNReal} : Ï†á¶œ = Ï† â‡¨ 0 := by simp [compl, himp]
example {Ï† : Î¹ â†’ ENNReal} : Ï†á¶œ = Ï† â‡¨ 0 := by simp [compl, himp]
example {Ï† : ENNReal} : ï¿¢Ï† = Ï† â†œ âŠ¤ := by simp [hnot]
example {Ï† : Î¹ â†’ ENNReal} : ï¿¢Ï† = Ï† â†œ âŠ¤ := by simp [hnot]
example {Ï† : ENNReal} : Ïˆ \ Ï† = Ï† â†œ Ïˆ := by simp [sdiff]

@[notation_class]
class Iverson (Î± : Type*) (Î² : outParam Type*) where
  /-- Iverson brackets `i[b]` -/
  iver : Î± â†’ Î²

@[inherit_doc] notation "i[" b "]" => Iverson.iver b

class LawfulIverson (Î± : Type*) (Î² : outParam Type*) [Iverson Î± Î²] [LE Î²] [One Î²] where
  iver_le_one : âˆ€ (b : Î±), i[b] â‰¤ 1

attribute [grind ., simp] LawfulIverson.iver_le_one

namespace Iverson

instance : Iverson Bool â„• := âŸ¨fun b â†¦ if b then 1 else 0âŸ©
instance : LawfulIverson Bool â„• := âŸ¨by simp [Iverson.iver]âŸ©
noncomputable instance : Iverson Prop â„• :=
  âŸ¨fun b â†¦ have : Decidable b := Classical.propDecidable _; if b then 1 else 0âŸ©
noncomputable instance : LawfulIverson Prop â„• := âŸ¨by simp [Iverson.iver]; grindâŸ©

@[grind =, simp] theorem iver_true : i[true] = 1 := by rfl
@[grind =, simp] theorem iver_True : i[True] = 1 := by simp [iver]
@[grind =, simp] theorem iver_false : i[false] = 0 := by rfl
@[grind =, simp] theorem iver_False : i[False] = 0 := by simp [iver]

@[grind .] theorem iver_neg : i[Â¬b] = 1 - i[b] := by simp [iver]; grind
@[grind .] theorem iver_not : i[!b] = 1 - i[b] := by simp [iver]; grind

end Iverson

export Iverson (iver)

section

variable {Î± Î² Î¹ : Type*} {Î³ : Î¹ â†’ Type*} [Substitution Î± Î³]

instance Pi.instSubstitution : Substitution (Î± â†’ Î²) fun (a : Î¹) â†¦ Î± â†’ Î³ a where
  subst f := fun âŸ¨i, xâŸ© Ïƒ â†¦ f Ïƒ[i â†¦ x Ïƒ]

@[grind =, simp]
theorem Pi.substs_cons (f : Î± â†’ Î²) (xâ‚€ : ((a : Î¹) Ã— (Î± â†’ Î³ a))) (xs : List ((a : Î¹) Ã— (Î± â†’ Î³ a))) :
  f[..xâ‚€ :: xs] Ïƒ = f[..xs] Ïƒ[xâ‚€.1 â†¦ xâ‚€.2 Ïƒ] := by rfl

end

namespace Pi

noncomputable instance instIverson {Î± : Type*} : Iverson (Î± â†’ Prop) (Î± â†’ ENNReal) where
  iver v := fun Ïƒ â†¦ i[v Ïƒ]
noncomputable instance instIversonBool {Î± : Type*} : Iverson (Î± â†’ Bool) (Î± â†’ ENNReal) where
  iver v := fun Ïƒ â†¦ i[v Ïƒ]
instance instLawfulIverson {Î± : Type*} : LawfulIverson (Î± â†’ Prop) (Î± â†’ ENNReal) where
  iver_le_one b := by intro Ïƒ; simp [instIverson]
instance instLawfulIversonBool {Î± : Type*} : LawfulIverson (Î± â†’ Bool) (Î± â†’ ENNReal) where
  iver_le_one b := by intro Ïƒ; simp [instIversonBool]
@[grind =, simp] theorem iver_apply {Î± : Type*} (f : Î± â†’ Prop) (a : Î±) :
    i[f] a = i[f a] := rfl
@[grind =, simp] theorem iver_subst {Î± Î¹ : Type*} {Î³ : Î¹ â†’ Type*}
    [Substitution Î± Î³]
    (f : Î± â†’ Prop) (x : Î¹) (y : Î± â†’ Î³ x) : i[f][x â†¦ y] = i[f[x â†¦ y]] := by
  rfl
@[grind =, simp] theorem iver_bool_apply {Î± : Type*} (f : Î± â†’ Bool) (a : Î±) :
    i[f] a = i[f a] := rfl
@[grind =, simp] theorem iver_bool_subst {Î± Î¹ : Type*} {Î³ : Î¹ â†’ Type*}
    [Substitution Î± Î³]
    (f : Î± â†’ Bool) (x : Î¹) (y : Î± â†’ Î³ x) : i[f][x â†¦ y] = i[f[x â†¦ y]] := by
  rfl

end Pi


/-! # Expressions & States -/

namespace pGCL

notation "Î“[" Ï– "]" => Ï– â†’ Type*
def States {ğ’± : Type*} (Î“ : Î“[ğ’±]) := (s : ğ’±) â†’ Î“ s

variable {ğ’± : Type*} {Ï– : Î“[ğ’±]}

notation "ğ”¼[" Ï– "," Î± "]" => States Ï– â†’ Î±

instance States.instSubstitution [DecidableEq ğ’±] : Substitution (States Ï–) Ï– where
  subst Ïƒ := fun âŸ¨v, tâŸ© Î± â†¦ if h : v = Î± then cast (congrArg Ï– h) t else Ïƒ Î±

@[ext] theorem States.ext {Ïƒâ‚ Ïƒâ‚‚ : States Ï–} (h : âˆ€ v, Ïƒâ‚ v = Ïƒâ‚‚ v) : Ïƒâ‚ = Ïƒâ‚‚ := _root_.funext h

@[grind =, simp] theorem States.subst_apply [DecidableEq ğ’±] {Ïƒ : States Ï–} :
    Ïƒ[x â†¦ v] y = if h : x = y then cast (congrArg Ï– h) v else Ïƒ y := rfl

namespace Exp

variable {a b : ğ”¼[Ï–, Î±]}

instance {Î¹ : Type*} [Add Î±] [LE Î±] [AddLeftMono Î±] : AddLeftMono (Î¹ â†’ Î±) := âŸ¨fun a b c h Ïƒ â†¦ by
  simp only [Pi.add_apply]; gcongr; apply hâŸ©
instance {Î¹ : Type*} [Add Î±] [LE Î±] [AddRightMono Î±] : AddRightMono (Î¹ â†’ Î±) := âŸ¨fun a b c h Ïƒ â†¦ by
  simp only [Pi.add_apply, Function.swap]; gcongr; apply hâŸ©

variable [DecidableEq ğ’±] {v : ğ’±} {e : ğ”¼[Ï–, Î±]}

@[simp] theorem add_subst [Add Î±] :
    (a + b)[..xs] = a[..xs] + b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem sub_subst [Sub Î±] :
    (a - b)[..xs] = a[..xs] - b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem mul_subst [Mul Î±] :
    (a * b)[..xs] = a[..xs] * b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem div_subst [Div Î±] :
    (a / b)[..xs] = a[..xs] / b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem max_subst [Max Î±] :
    (a âŠ” b)[..xs] = a[..xs] âŠ” b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem min_subst [Min Î±] :
    (a âŠ“ b)[..xs] = a[..xs] âŠ“ b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem himp_subst [HImp Î±] :
    (a â‡¨ b)[..xs] = a[..xs] â‡¨ b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl
@[simp] theorem sdiff_subst [SDiff Î±] :
    (a â†œ b)[..xs] = a[..xs] â†œ b[..xs] := Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl

omit [DecidableEq ğ’±]

@[grind =, simp] theorem validate_apply [HNot Î±] {Ï† : ğ”¼[Ï–, Î±]} :
    (â–µ Ï†) Ïƒ = â–µ Ï† Ïƒ := rfl
@[grind =, simp] theorem covalidate_apply [Compl Î±] {Ï† : ğ”¼[Ï–, Î±]} :
    (â–¿ Ï†) Ïƒ = â–¿ Ï† Ïƒ := rfl

@[grind =, simp]
theorem subst_apply [DecidableEq ğ’±] (e : ğ”¼[Ï–, Î±]) (x : ğ’±) (A : ğ”¼[Ï–, Ï– x]) :
    e[x â†¦ A] Ïƒ = e (Ïƒ[x â†¦ A Ïƒ]) := rfl
@[grind =, simp]
theorem substâ‚‚_apply [DecidableEq ğ’±] (e : ğ”¼[Ï–, Î±]) (x : ğ’±) (A : ğ”¼[Ï–, Ï– x]) :
    e[x â†¦ A, y â†¦ B] Ïƒ = e (Ïƒ[y â†¦ B Ïƒ[x â†¦ A Ïƒ], x â†¦ A Ïƒ]) := by
  simp_all [Substitution.substs_cons, Substitution.subst]

@[gcongr]
theorem add_le_add [Add Î±] [Preorder Î±] [AddLeftMono Î±] [AddRightMono Î±] (a b c d : ğ”¼[Ï–, Î±])
    (hac : a â‰¤ c) (hbd : b â‰¤ d) : a + b â‰¤ c + d := by
  intro; simp; gcongr <;> apply_assumption
@[gcongr]
theorem mul_le_mul [Mul Î±] [Preorder Î±] [MulLeftMono Î±] [MulRightMono Î±] (a b c d : ğ”¼[Ï–, Î±])
    (hac : a â‰¤ c) (hbd : b â‰¤ d) : a * b â‰¤ c * d := by
  intro; simp; gcongr <;> apply_assumption

end Exp

abbrev BExpr (Ï– : Î“[ğ’±]) := ğ”¼[Ï–, Prop]

namespace BExpr

noncomputable def forall_ [DecidableEq ğ’±] (x : ğ’±) (b : BExpr Ï–) : BExpr Ï– :=
  fun Ïƒ â†¦ âˆ€ (v : Ï– x), b Ïƒ[x â†¦ v]
noncomputable def exists_ [DecidableEq ğ’±] (x : ğ’±) (b : BExpr Ï–) : BExpr Ï– :=
  fun Ïƒ â†¦ âˆƒ (v : Ï– x), b Ïƒ[x â†¦ v]

instance : HNot (BExpr Î±) where hnot := compl

variable {b : BExpr Ï–}

@[coe] def coe_prop : Prop â†’ BExpr Ï– := fun b â†¦ fun _ â†¦ b
instance : Coe Prop ğ”¼[Ï–, Prop] := âŸ¨coe_propâŸ©
@[grind =, simp] theorem coe_prop_apply {b} : coe_prop (Ï–:=Ï–) b Ïƒ = b := by rfl

@[simp] theorem iver_mul_le_apply {X : ğ”¼[Ï–, ENNReal]} : i[b Ïƒ] * X Ïƒ â‰¤ X Ïƒ := by calc
  _ â‰¤ 1 * X Ïƒ := by gcongr; simp
  _ = _ := by simp
@[grind ., simp] theorem iver_mul_le : i[b] * X â‰¤ X := by intro; simp
@[simp] theorem mul_iver_le_apply {X : ğ”¼[Ï–, ENNReal]} : X Ïƒ * i[b Ïƒ] â‰¤ X Ïƒ := by calc
  _ â‰¤ X Ïƒ * 1 := by gcongr; simp
  _ = _ := by simp
@[grind ., simp] theorem mul_iver_le : X * i[b] â‰¤ X := by intro; simp

variable [PartialOrder Î±]

noncomputable def lt (l r : ğ”¼[Ï–, Î±]) : BExpr Ï– :=
  fun Ïƒ â†¦ l Ïƒ < r Ïƒ
noncomputable def le (l r : ğ”¼[Ï–, Î±]) : BExpr Ï– :=
  fun Ïƒ â†¦ l Ïƒ â‰¤ r Ïƒ
noncomputable def eq (l r : ğ”¼[Ï–, Î±]) : BExpr Ï– :=
  fun Ïƒ â†¦ l Ïƒ = r Ïƒ
def and (l r : BExpr Ï–) : BExpr Ï– := fun Ïƒ â†¦ l Ïƒ âˆ§ r Ïƒ
def or (l r : BExpr Ï–) : BExpr Ï– := fun Ïƒ â†¦ l Ïƒ âˆ¨ r Ïƒ
noncomputable def ite (b l r : BExpr Ï–) : BExpr Ï– :=
  letI := Classical.decPred b
  fun Ïƒ â†¦ if (b Ïƒ) then l Ïƒ else r Ïƒ

@[grind =, simp] theorem lt_apply {l r : ğ”¼[Ï–, Î±]} : lt l r Ïƒ â†” l Ïƒ < r Ïƒ := by rfl
@[grind =, simp] theorem le_apply {l r : ğ”¼[Ï–, Î±]} : le l r Ïƒ â†” l Ïƒ â‰¤ r Ïƒ := by rfl
omit [PartialOrder Î±] in
@[grind =, simp] theorem eq_apply {l r : ğ”¼[Ï–, Î±]} : eq l r Ïƒ â†” l Ïƒ = r Ïƒ := by rfl
@[grind =, simp] theorem and_apply {l r : BExpr Ï–} : and l r Ïƒ â†” l Ïƒ âˆ§ r Ïƒ := by rfl
@[grind =, simp] theorem or_apply {l r : BExpr Ï–} : or l r Ïƒ â†” l Ïƒ âˆ¨ r Ïƒ := by rfl
open scoped Classical in
@[grind =, simp] theorem ite_apply (b l r : BExpr Ï–) : ite b l r Ïƒ = if b Ïƒ then l Ïƒ else r Ïƒ := rfl

omit [PartialOrder Î±] in
@[simp] theorem eq_subst [DecidableEq ğ’±] {a b : ğ”¼[Ï–, Î±]} :
    (BExpr.eq a b)[..xs] = BExpr.eq a[..xs] b[..xs] :=
  Substitution.substs_of_binary fun _ _ â†¦ congrFun rfl

end BExpr

end pGCL

open pGCL

namespace OrderHom

variable {Î± Î² : Type*} [Preorder Î±] [Preorder Î²] [Add Î²] [AddLeftMono Î²] [AddRightMono Î²]

instance : AddLeftMono (States Ï– â†’ ENNReal) where
  elim a _ _ hbc := fun Ïƒ â†¦ add_le_add_right (hbc Ïƒ) (a Ïƒ)
instance : AddRightMono (States Ï– â†’ ENNReal) where
  elim a _ _ hbc := fun Ïƒ â†¦ add_le_add_left (hbc Ïƒ) (a Ïƒ)

instance instAdd : Add (Î± â†’o Î²) where
  add a b := âŸ¨fun x â†¦ a x + b x, fun x y h â†¦ by simp; gcongrâŸ©
@[simp] theorem add_apply (f g : Î± â†’o Î²) : (f + g) x = f x + g x := by rfl
@[simp] theorem add_apply2' (f g : Î± â†’o States Ï– â†’ ENNReal) : (f + g) x y = f x y + g x y := by rfl

instance [OfNat Î² n] : OfNat (Î± â†’o Î²) n := âŸ¨fun _ â†¦ OfNat.ofNat n, by intro; simpâŸ©
omit [Add Î²] [AddLeftMono Î²] [AddRightMono Î²] in
@[simp]
theorem ofNat_apply [OfNat Î² n] : (ofNat(n) : Î± â†’o Î²) a = ofNat(n) := by rfl

instance {Î² : Type*} [Preorder Î²] [AddZeroClass Î²] [AddLeftMono Î²] [AddRightMono Î²] :
    AddZeroClass (Î± â†’o Î²) where
  zero_add f := by ext; simp
  add_zero f := by ext; simp

instance {Î± Î² : Type*} [Preorder Î²] [Add Î²] [i : AddRightMono Î²] : AddRightMono (Î± â†’ Î²) where
  elim a b c h i := by simp [Function.swap]; gcongr; apply h
instance {Î± Î² : Type*} [Preorder Î²] [Add Î²] [i : AddLeftMono Î²] : AddLeftMono (Î± â†’ Î²) where
  elim a b c h i := by simp only [Pi.add_apply]; gcongr; apply h

variable {Î¹ : Type*}

omit [Add Î²] [AddLeftMono Î²] [AddRightMono Î²] in
@[simp, grind =]
theorem mk_apply {f} {h} {b : Î¹} :
    ({toFun := f, monotone' := h} : Î± â†’o (Î¹ â†’ Î²)) a b = f a b := by rfl
omit [Add Î²] [AddLeftMono Î²] [AddRightMono Î²] in
@[simp, grind =]
theorem mk_apply' {f} {h} {b : Î¹} :
    DFunLike.coe ({toFun := f, monotone' := h} : Î± â†’o (Î¹ â†’ Î²)) a b = f a b := by rfl
omit [Add Î²] [AddLeftMono Î²] [AddRightMono Î²] in
@[simp, grind =]
theorem comp_apply' {Î¹ : Type*} {Î³ : Type*} [Preorder Î³] {f : Î² â†’o (Î¹ â†’ Î³)} {g : Î± â†’o Î²} {b : Î¹} :
    (OrderHom.comp f g) a b = f (g a) b := rfl

end OrderHom
